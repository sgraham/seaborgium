# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

#ar = "C:/Program Files (x86)/LLVM/bin/clang-cl.exe"
#cc = "C:/Program Files (x86)/LLVM/bin/clang-cl.exe"
#cxx = "C:/Program Files (x86)/LLVM/bin/clang-cl.exe"
ar = "cl.exe"
cc = "cl.exe"
cxx = "cl.exe"
ld = "link.exe"
helper_path = rebase_path("helper.py")

toolchain("win") {
  tool("cc") {
    depfile = "{{output}}.d"
    command = "$cc /nologo /showIncludes {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} /c {{source}} /Fo{{output}}"
    depsformat = "msvc"
    description = "CC {{output}}"
    outputs = [
      "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.obj",
    ]
  }
  tool("cxx") {
    depfile = "{{output}}.d"
    command = "$cxx /nologo /showIncludes {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} /c {{source}} /Fo{{output}}"
    depsformat = "msvc"
    description = "CXX {{output}}"
    outputs = [
      "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.obj",
    ]
  }
  tool("rc") {
    command = "rc.exe /nologo {{defines}} {{include_dirs}} /fo{{output}} {{source}}"
    outputs = [
      "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.res",
    ]
    description = "RC {{output}}"
  }
  tool("alink") {
    command = "$ar {{output}} {{inputs}}"
    description = "LIB {{output}}"
    outputs = [
      "{{target_out_dir}}/{{target_output_name}}{{output_extension}}",
    ]
    default_output_extension = ".lib"
    output_prefix = ""
  }
  tool("link") {
    outputs = [
      "{{root_out_dir}}/{{target_output_name}}{{output_extension}}",
    ]
    command = "$ld /nologo /OUT:{{output}} {{ldflags}} {{inputs}} {{solibs}} {{libs}}"
    description = "LINK {{output}}"
    default_output_extension = ".exe"
  }
  tool("stamp") {
    command = "$python_path $helper_path stamp {{output}}"
    description = "STAMP {{output}}"
  }
  tool("copy") {
    command = "$python_path $helper_path recursive-mirror {{source}} {{output}}"
    description = "COPY {{source}} {{output}}"
  }
}
